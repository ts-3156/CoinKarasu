buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
        maven { url 'https://maven.google.com' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.google.gms.oss.licenses.plugin'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

Properties fabricProps = new Properties()
try {
    fabricProps.load(file('fabric.properties').newDataInputStream())
} catch (Exception e) {
    throw new GradleException(e.getMessage())
}

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.coinkarasu"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 19
        versionName "1.0.3"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        manifestPlaceholders = [fabricApiKey: "${fabricProps.getProperty("apiKey")}"]
        buildConfigField "String", "PK1", "\"${billing_pkey1}\""
        buildConfigField "String", "PK2", "\"${billing_pkey2}\""
        buildConfigField "String", "ADMOB_APP_ID", "\"${admob_dummy_app_id}\""
        buildConfigField "String", "ADMOB_UNIT_ID", "\"${admob_dummy_ad_unit_id}\""
        buildConfigField "String", "CK_TMP_KEY", "\"${ck_api_key}\""
        buildConfigField "String", "CK_TMP_SECRET", "\"${ck_api_secret}\""
        buildConfigField "String", "CK_HOST", "\"${ck_host}\""
        buildConfigField("String", "SAFETYNET_API_KEY", "\"${safetynet_api_key}\"")
        resValue "string", "facebook_app_id", "${facebook_app_id}"
    }
    buildTypes {
        debug {
        }
        release {
            buildConfigField "String", "ADMOB_APP_ID", "\"${admob_app_id}\""
            buildConfigField "String", "ADMOB_UNIT_ID", "\"${admob_ad_unit_id}\""
            buildConfigField "String", "CK_HOST", "\"http://coinkarasu.com\""
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            lintOptions {
                disable 'MissingTranslation'
            }
        }
    }
    repositories {
        maven { url "https://jitpack.io" }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:27.0.2'
    implementation 'com.android.support:design:27.0.2'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.android.support:support-v4:27.0.2'
    implementation 'com.google.firebase:firebase-core:11.8.0'
    implementation 'com.google.firebase:firebase-messaging:11.8.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    implementation 'com.android.volley:volley:1.0.0'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.3'
    implementation 'com.android.support:cardview-v7:27.0.2'
    implementation('com.crashlytics.sdk.android:crashlytics:2.8.0@aar') {
        transitive = true
    }
    implementation 'com.google.android.gms:play-services-gcm:11.8.0'
    implementation "android.arch.persistence.room:runtime:1.0.0"
    annotationProcessor "android.arch.persistence.room:compiler:1.0.0"
    implementation 'com.google.android.gms:play-services-oss-licenses:11.8.0'
    implementation('com.google.http-client:google-http-client:1.19.0') {
        exclude group: 'com.google.code.findbugs'
    }
    implementation('com.google.http-client:google-http-client-jackson2:1.23.0') {
        exclude group: 'com.google.code.findbugs'
    }
    implementation 'com.android.billingclient:billing:1.0'
    implementation 'com.google.android.gms:play-services-ads:11.8.0'
    implementation 'com.github.ts-3156:MaterialShowcaseView:1.2.6'
    implementation "com.android.support:transition:27.0.2"
    implementation 'com.robinhood.spark:spark:1.2.0'
    implementation 'com.google.android.gms:play-services-safetynet:11.8.0'
    implementation 'com.facebook.android:facebook-android-sdk:[4,5)'
}

configurations {
    all {
        exclude module: 'commons-logging'
        exclude module: 'httpclient'
    }
}

apply plugin: 'com.google.gms.google-services'